def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
}
else {
    apply plugin: 'com.android.library'
}
android {
    compileSdk 30

    defaultConfig {
        applicationId "com.example.unnamed_sdvx_clone"
        minSdk 30
        targetSdk 30
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters System.env.ANDROID_ABI
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                def ndkDir = android.ndkDirectory
                
                cppFlags '-std=c++17'
                arguments "-DANDROID_STL=c++_shared", 
                          "-DANDROID_SYSROOT_PATH=$ndkDir/sysroot/usr", 
                          "-DANDROID=1", 
                          "-DANDROID_NDK_PLATFORM=$ndkDir/platforms/android-$targetSdk/$System.env.NDK_PLATFORM_ARCH/usr/lib",
                          "-DCMAKE_TOOLCHAIN_FILE=$vcpkg_toolchain_file",
                          "-DVCPKG_CHAINLOAD_TOOLCHAIN_FILE=$android_toolchain_file",
                          "-DVCPKG_TARGET_TRIPLET=$System.env.VCPKG_TARGET_TRIPLET",
                          "-DANDROID_ABI=$System.env.ANDROID_ABI"
            }
            // ndkBuild {
            //     arguments "APP_PLATFORM=android-28"
            //     abiFilters 'arm64-v8a'
            // }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir 'src/main/cpp/bin'
            assets.srcDir 'src/main/assets'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        
        debug {
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
        // ndkBuild {
        //     path 'src/main/cpp/arm64-v8a/Android.mk'
        // }
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion ndkVersion

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "org.libsdl.app.aar";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    def appcompat_version = "1.3.0"
    def activity_version = "1.3.1"

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.activity:activity:$activity_version"
}